plugins {
  id 'org.springframework.boot' version '2.7.0-RC1'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id 'idea'
  // TODO: check later - AOP plugin incompatible with 2.7.0-M3
  // id 'org.springframework.experimental.aot' version '0.11.4'
  id 'de.undercouch.download' version '5.1.0'
  id 'io.openapiprocessor.openapi-processor' version '2022.2'
  id 'com.gorylenko.gradle-git-properties' version '2.4.1'
}

group = 'io.kong.developer'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

springBoot {
  mainClass = "io.kong.developer.apiops.APIOpsWorkshopApplication"
  buildInfo()
}
repositories {
  maven { url 'https://repo.spring.io/release' }
  maven { url 'https://repo.spring.io/milestone' }
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.mapstruct:mapstruct:1.4.2.Final'
  implementation 'org.apache.commons:commons-lang3:3.12.0'
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
  annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

  runtimeOnly 'com.h2database:h2'
  runtimeOnly 'io.r2dbc:r2dbc-h2'

  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.projectreactor:reactor-test'

  testImplementation 'org.assertj:assertj-core:3.22.0'
  // If you are using mapstruct in test code
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"
}

tasks.named('test') {
  useJUnitPlatform()
}

tasks.named('bootBuildImage') {
  builder = 'paketobuildpacks/builder:tiny'
  //environment = ['BP_NATIVE_IMAGE': 'true']
}

/* TODO: make API spec available in GH */
/*task downloadFile(type: Download) {
  src "https://raw.githubusercontent.com/codecentric/api-showcases/main/specs/news.yaml"
  dest "${projectDir}/src/api/news.yaml"
  onlyIfModified true
  useETag true
}*/

openapiProcessor {
  spring {
    processor 'io.openapiprocessor:openapi-processor-spring:2022.3'
    apiPath "$projectDir/src/main/resources/spec/devnexus-api-v1.0.yaml"
    targetDir "$projectDir/build/openapi"
    mapping "$projectDir/src/main/resources/mapping.yaml"
    showWarnings true
  }
}

afterEvaluate {
  //tasks.processSpring.dependsOn(tasks.downloadFile)
}

sourceSets {
  main {
    java {
      srcDir "build/openapi"
    }
  }

  test {
    resources {
      srcDir file('src/test/java')
      exclude '**/*.java'
    }
  }
}

compileJava.dependsOn('processSpring')
