plugins {
  id 'org.springframework.boot' version '3.0.1'
  id 'io.spring.dependency-management' version '1.0.13.RELEASE'
  id 'java'
  id 'idea'
  // TODO: check later - AOP plugin incompatible with 2.7.0-M3
  // id 'org.springframework.experimental.aot' version '0.11.4'
  id 'de.undercouch.download' version '5.2.0'
  id 'io.openapiprocessor.openapi-processor' version '2022.2'
  id 'com.gorylenko.gradle-git-properties' version '2.4.1'
}

group = 'io.kong.developer'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

springBoot {
  mainClass = "io.kong.developer.apiops.APIOpsWorkshopApplication"
  buildInfo()
}
repositories {
  maven { url 'https://repo.spring.io/release' }
  maven { url 'https://repo.spring.io/milestone' }
  mavenCentral()
}

dependencies {
  // If you are using mapstruct in test code
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'
  annotationProcessor 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
  
  compileOnly 'org.projectlombok:lombok'
  
  implementation 'org.apache.commons:commons-lang3:3.12.0'
  implementation 'org.mapstruct:mapstruct:1.5.2.Final'
  implementation 'org.postgresql:r2dbc-postgresql:0.9.2.RELEASE'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
  implementation 'org.springframework.boot:spring-boot-starter-graphql'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.5.2.Final"
  testAnnotationProcessor 'org.projectlombok:lombok'
  
  testCompileOnly 'org.projectlombok:lombok'
  
  testImplementation 'io.projectreactor:reactor-test'
  testImplementation 'org.assertj:assertj-core:3.23.1'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.graphql:spring-graphql-test'
  testImplementation 'org.testcontainers:postgresql:1.17.3'
  testImplementation 'org.testcontainers:r2dbc:1.17.3'
}

tasks.withType(JavaCompile) {
  options.compilerArgs = [
      '-Xlint:unchecked',
      '-Xlint:deprecation',
      '-Werror'
  ]
}

tasks.named('test') {
  useJUnitPlatform()

  testLogging {
    outputs.upToDateWhen { false }
    showStandardStreams = false
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
}

tasks.named('bootBuildImage') {
  builder = 'paketobuildpacks/builder:tiny'
  //environment = ['BP_NATIVE_IMAGE': 'true']
}

/* TODO: make API spec available in GH */
/*task downloadFile(type: Download) {
  src "https://raw.githubusercontent.com/codecentric/api-showcases/main/specs/news.yaml"
  dest "${projectDir}/src/api/news.yaml"
  onlyIfModified true
  useETag true
}*/

openapiProcessor {
  spring {
    processor 'io.openapiprocessor:openapi-processor-spring:2022.4'
    apiPath "$projectDir/src/main/resources/spec/devnexus-api-v1.0.yaml"
    targetDir "$projectDir/build/openapi"
    mapping "$projectDir/src/main/resources/mapping.yaml"
    showWarnings true
  }
}

afterEvaluate {
  //tasks.processSpring.dependsOn(tasks.downloadFile)
}

sourceSets {
  main {
    java {
      srcDir "build/openapi"
    }
  }

  test {
    resources {
      srcDir file('src/test/java')
      exclude '**/*.java'
    }
  }
}

compileJava.dependsOn('processSpring')
